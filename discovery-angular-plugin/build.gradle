apply plugin: 'com.moowork.node'
evaluationDependsOn(':discovery-angular')

node {
    version = '8.10.0'
    npmVersion = '5.6.0'
    yarnVersion = '1.5.1'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file("${rootProject.buildDir}/nodejs")
    npmWorkDir = file("${rootProject.buildDir}/npm")
    yarnWorkDir = file("${rootProject.buildDir}/yarn")
    nodeModulesDir = file("${projectDir}/module")
}

task angularTest(type: YarnTask) {
    workingDir = file("${projectDir}/module")
    args = ["test"]
}

task generateDemo(type: Zip, dependsOn: angularTest) {
    from("$projectDir/module/integration-test/big-multi-module") {
        include "frontend-landscape.json"
    }
    archiveName 'demo-frontend.zip'
}

task publishAngularPlugin(type: YarnTask, dependsOn: build) {
    workingDir = file("${projectDir}/module")
    args = ["publish", "--no-git-tag-version", "--new-version", "${->getNonSnapshotVersion()}", "--access", "public"]
}

task unpublishAngularPlugin(type: NpmTask) {
    workingDir = file("${project.projectDir}/module")
    args = ["publish","@landzcape/discovery-angular@${->getNonSnapshotVersion()}"]
}

task pack(type: NpmTask) {
    workingDir = file("${project.projectDir}/module")
    args = ["pack"]
}

task importDomain(type: Sync, dependsOn: project(':discovery-angular').assembleDiscovery) {
    from zipTree(project(':discovery-angular').assembleDiscovery.outputs.files[0])
    into "${project.projectDir}/module/discover/domain"
}

def getNonSnapshotVersion() {
    if(project.version.endsWith('SNAPSHOT')) {
        throw new RuntimeException('A tag version must be specified')
    }
    return project.version
}

angularTest.dependsOn(yarn)
yarn.dependsOn(importDomain)
test.dependsOn(angularTest)
build.dependsOn(yarn)
build.dependsOn(angularTest)