apply plugin: 'com.moowork.node'
apply plugin: 'maven'

node {
    version = '8.10.0'
    npmVersion = '5.6.0'
    yarnVersion = '1.5.1'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file("${rootProject.buildDir}/nodejs")
    npmWorkDir = file("${rootProject.buildDir}/npm")
    yarnWorkDir = file("${rootProject.buildDir}/yarn")
    nodeModulesDir = file("${project.projectDir}/webapp")
}

task start(type: YarnTask) {
    workingDir = file("${project.projectDir}/webapp")
    args = ["start"]
}

task landscapeExplorerPackage(type: Zip, dependsOn: yarn_build) {
    from 'webapp/dist'
    include '*'
    include '*/*'
    archiveName "landscape-explorer.zip"
}

task addExplorerPackageToResources(type: Sync, dependsOn: landscapeExplorerPackage) {
    from "$buildDir/distributions/landscape-explorer.zip"
    into "src/main/resources"
}

clean {
    delete 'webapp/dist'
    delete 'src/main/resources/landscape-explorer.zip'
}

yarn_build.inputs.dir 'webapp/src'
yarn_build.inputs.dir 'webapp/e2e'
yarn.inputs.file 'webapp/package.json'
yarn.outputs.dir 'webapp/node_modules'
yarn.outputs.file 'webapp/yarn.lock'
yarn_build.outputs.dir 'webapp/dist'
yarn_build.dependsOn(yarn)
processResources.dependsOn(addExplorerPackageToResources)
