apply plugin: 'com.moowork.node'
apply plugin: 'maven'

node {
    version = '8.10.0'
    npmVersion = '5.6.0'
    yarnVersion = '1.5.1'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file("${rootProject.buildDir}/nodejs")
    npmWorkDir = file("${rootProject.buildDir}/npm")
    yarnWorkDir = file("${rootProject.buildDir}/yarn")
    nodeModulesDir = file("${project.projectDir}/webapp")
}

task start(type: YarnTask) {
    workingDir = file("${project.projectDir}/webapp")
    args = ["start"]
}

task landscapeWebPackage(type: Zip, dependsOn: yarn_build) {
    from 'webapp/dist'
    include '*'
    include '*/*'
    archiveName "landscape-web.zip"
}

task addWebPackageToResources(type: Sync, dependsOn: landscapeWebPackage) {
    from "$buildDir/distributions/landscape-web.zip"
    into "src/main/resources"
}

clean {
    delete 'webapp/dist'
    delete 'src/main/resources/landscape-web.zip'
}

processResources.dependsOn(addWebPackageToResources)
