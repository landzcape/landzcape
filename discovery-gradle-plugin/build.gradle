apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(':discovery-domain')
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.9.3"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

clean {
    delete 'src/main/resources/landscape-explorer.zip'
}

task prepareExplorer(type: Copy, dependsOn: project(':explorer').jar) {
    from zipTree(project(':explorer').jar.outputs.files[0]).asFileTree
    into "$projectDir/src/main/resources"
    include 'landscape-explorer.zip'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

processResources.dependsOn('prepareExplorer')
test.dependsOn(':discovery-domain:install')

publishing {
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        discoveryGradlePlugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Landzcape Discovery Gradle Plugin'
                packaging = 'jar'
                description = 'Landzcape Discovery Plugin for Gradle'
                url = 'https://www.landzcape.io'

                licenses {
                    license {
                        name = 'GNU General Public License (GPLV3+)'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/landzcape/landzcape'
                    developerConnection = 'scm:git:https://github.com/landzcape/landzcape'
                    url = 'https://github.com/landzcape/landzcape'
                }

                developers {
                    developer {
                        id = 'christian'
                        name = 'Christian Ramseier'
                        email = 'christian@landzcape.io'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.discoveryGradlePlugin
}

task publishGradlePlugin(dependsOn: publishDiscoveryGradlePluginPublicationToMavenRepository) {}

task('promoteNexusRelease').mustRunAfter publishGradlePlugin